"""sm-calling: Somatic and germline variant calling pipeline.

Requires Snakemake 8.0+. Uses the SLURM executor plugin for cluster execution.
"""

from snakemake.utils import min_version, validate

min_version("8.0")

import pandas as pd


# -- Configuration -----------------------------------------------------------
configfile: "config/config.yaml"


validate(config, "schemas/config.schema.yaml")

samples_df = pd.read_table(config["paths"]["samples"]).set_index("sample", drop=False)
validate(samples_df, "schemas/samples.schema.yaml")


# -- Includes ----------------------------------------------------------------
include: "rules/common.smk"
include: "rules/scatter.smk"
include: "rules/mutect2.smk"
include: "rules/freebayes.smk"
include: "rules/qc.smk"
include: "rules/purecn.smk"


# -- Validation --------------------------------------------------------------
if PURECN_ENABLED and CALLER in ("mutect2", "all"):
    if not PURECN_INTERVALS_BED:
        raise ValueError(
            "PureCN is enabled but 'purecn.intervals_bed' is not set. "
            "Provide a BED file with capture bait coordinates."
        )
    if not get_normal_bams() and not PURECN_NORMALDB:
        raise ValueError(
            "PureCN is enabled but no normal BAMs found in samples.tsv "
            "and no pre-built normalDB provided (purecn.normaldb). "
            "Either add normal BAMs to your sample sheet or provide a "
            "pre-built normalDB.rds via the purecn.normaldb config field."
        )


# -- Target rule -------------------------------------------------------------
rule all:
    localrule: True
    input:
        get_final_outputs(),
